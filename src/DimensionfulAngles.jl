"""
Extends Unitful.jl to include Angle as an independent dimension in order to facilitate
[dispatching](https://docs.julialang.org/en/v1/manual/methods/#Methods).

See the [Documentation](https://cmichelenstrofer.github.io/DimensionfulAngles/) for more
information.

!!! note "Not SI"

    *Angle* is not an SI base dimension.

# Examples

```jldoctest
julia> using DimensionfulAngles

julia> 1.0ua"turn"
1.0 œÑ

julia> 1.0ua"rad" - 1.0ua"¬∞"
0.9825467074800567 rad

julia> cos(45ua"¬∞")
0.7071067811865476
```
"""
module DimensionfulAngles

# using Base: Base  # extend: see `base.jl` for full list of functions extended
using Unitful: Unitful  # extend: has_unit_spacing, uconvert
using Unitful: minute, promotion, rad, rpm, rps, s, sr, ùêì, ùêã, ¬∞
using Unitful: Dimension, Dimensions, DimensionlessQuantity, FreeUnits, Frequency
using Unitful: FrequencyFreeUnits, Length, MixedUnits, NoDims, NoUnits, Number, Quantity
using Unitful: Time, Unitlike, Unit, Units, Wavenumber
using Unitful: @dimension, @refunit, @derived_dimension, @unit
using Unitful: dimension, register, uconvert, unit, ustrip
using UnitfulEquivalences: Equivalence, @eqrelation
using UnitfulAngles: turn, doubleTurn, halfTurn, quadrant, sextant, octant, clockPosition
using UnitfulAngles: hourAngle, compassPoint, hexacontade, brad, diameterPart, grad
using UnitfulAngles: arcminute, arcsecond
# using UnitfulAngles: mas, Œºas, pas # UnitfulAngles PR #34

export @ua_str
export Œ∏‚ÇÄ
export Periodic
export sexagesimal, show_sexagesimal
# export ùêÄ, Angle, AngleUnits, AngleFreeUnits
# export SolidAngle, SolidAngleUnits, SolidAngleFreeUnits
# export AngularVelocity, AngularVelocityUnits, AngularVelocityFreeUnits
# export AngularAcceleration, AngularAccelerationUnits, AngularAccelerationFreeUnits
# export AngularPeriod, AngularPeriodUnits, AngularPeriodFreeUnits
# export AngularWavenumber, AngularWavenumberUnits, AngularWavenumberFreeUnits
# export AngularWavelength, AngularWavelengthUnits, AngularWavelengthFreeUnits

"""
    ùêÄ

A dimension representing Angle.

!!! note "Not SI"

    *Angle* is not an SI base dimension.
"""
@dimension ùêÄ "ùêÄ" Angle true

# SI units
"""
    rad·µÉ

The radian, a unit of angle.

There are 2œÄ rad in a circle.
The radian is the SI unit of angle.
Unlike `Unitful.rad`, which follows SI and is therefor dimensionless,
`rad·µÉ` has dimensions of Angle.
Accepts SI prefixes.

Dimension: [`DimensionfulAngles.ùêÄ`](@ref).

# Examples

```jldoctest
julia> using DimensionfulAngles

julia> 1.0ua"rad" + 20.0ua"mrad"
1.02 rad
```
"""
@refunit rad·µÉ "rad" Radian·µÉ ùêÄ true true

"""
    ¬∞·µÉ

The degree, a unit of angle.

There are 360¬∞ in a circle.
The degree is an SI-accepted unit of angle.
Unlike `Unitful.¬∞`, which follows SI and is therefor dimensionless,
`¬∞·µÉ` has dimensions of Angle.
Does not accepts SI prefixes.

Dimension: [`DimensionfulAngles.ùêÄ`](@ref).

# Examples

```jldoctest
julia> using DimensionfulAngles

julia> 1ua"¬∞"
1¬∞
```
"""
@unit ¬∞·µÉ "¬∞" Degree·µÉ (1rad·µÉ * œÄ/180) false
Unitful.has_unit_spacing(u::Units{(Unit{:Degree·µÉ, ùêÄ}(0, 1 // 1),), ùêÄ}) = false

# Constants
"""
    Œ∏‚ÇÄ

A quantity equal to the central angle of a plane circular sector whose arc length is
equal to that of its radius.
It has a value of exactly 1 rad or approximately 57.2958¬∞.
Used as the defining constant of Angle dimension in several proposed SI extension systems.

Dimensions: ùêÄ.

See also [`DimensionfulAngles.rad·µÉ`](@ref).

# Examples

```jldoctest
julia> using DimensionfulAngles

julia> Œ∏‚ÇÄ
1//1 rad

julia> Œ∏‚ÇÄ |> ua"¬∞"
57.29577951308232¬∞

julia> 2.1ua"rad" / Œ∏‚ÇÄ
2.1
```
"""
const Œ∏‚ÇÄ = (1 // 1)rad·µÉ

# Convert to/from Unitful (SI)
function _convert_angles(x::Quantity, input_angle_unit::Units, input_dim::Dimensions,
    output_angle_unit::Units, angle_units::NTuple{N, Units} where N,
    )
    # convert all units in `angle_units` to common `input_angle_unit` unit.
    # cannot use `upreferred` because of `FixedUnits` and `ContextUnits`
    AngularUnits = [
        typeof(typeof(angle_unit).parameters[1][1]) for angle_unit in angle_units
    ]

    power = 0//1
    input_units = 1
    for iunit in typeof(x).parameters[3].parameters[1]
        ipower = iunit.power
        if typeof(iunit) ‚àà AngularUnits
            power += ipower
            input_units *= uconvert(
                input_angle_unit^ipower,
                1Unitful.FreeUnits{(iunit,), input_dim^ipower, nothing}()
            )
        else
            input_units *= (
                1Unitful.FreeUnits{(iunit,), typeof(iunit).parameters[2]^ipower, nothing}()
            )
        end
    end
    input_units = unit(input_units)
    x = uconvert(input_units, x)
    # convert to output units/dimensions
    return x*input_angle_unit^-power*output_angle_unit^power
end

Unitful.uconvert(s::Symbol, x::Quantity) = Unitful.uconvert(Val{s}(), x)

function Unitful.uconvert(s::Val{:Unitful}, x::Quantity)
    angle_units = (
        rad·µÉ, ¬∞·µÉ, turn·µÉ, doubleTurn·µÉ, halfTurn·µÉ, quadrant·µÉ, sextant·µÉ, octant·µÉ,
        clockPosition·µÉ, hourAngle·µÉ, compassPoint·µÉ, hexacontade·µÉ, brad·µÉ, diameterPart·µÉ,
        grad·µÉ, arcminute·µÉ, arcsecond·µÉ, as·µÉ,  ∞·µÉ, ·µê·µÉ, À¢·µÉ,
    )
    x = _convert_angles(x, rad·µÉ, ùêÄ, rad, angle_units)
    # derived units that contain angles.
    x = _convert_angles(x, sr·µÉ, ùêÄ^2, sr, (sr·µÉ,))
    x = _convert_angles(x, rps·µÉ, ùêÄ*ùêì^-1, rps, (rps·µÉ,))
    x = _convert_angles(x, rpm·µÉ, ùêÄ*ùêì^-1, rpm, (rpm·µÉ,))
    return x
end

function Unitful.uconvert(s::Val{:DimensionfulAngles}, x::Quantity)
    angle_units = (
        rad, ¬∞, turn, doubleTurn, halfTurn, quadrant, sextant, octant, clockPosition,
        hourAngle, compassPoint, hexacontade, brad, diameterPart, grad, arcminute,
        arcsecond,
        # TODO: mas, Œºas, pas  # UnitfulAngles PR #34
    )
    x = _convert_angles(x, rad, NoDims, rad·µÉ, angle_units)
    # derived units that contain angles.
    x = _convert_angles(x, sr, NoDims, sr·µÉ, (sr,))
    x = _convert_angles(x, rps, NoDims, rps·µÉ, (rps,))
    x = _convert_angles(x, rpm, NoDims, rpm·µÉ, (rpm,))
    return x
end

# Other functionalities.
include("units.jl")  # Other units of angle.
include("derived.jl")  # Units and functionalities for derived dimensions.
include("uamacro.jl")  # String macro for using dimensionful units.
include("base.jl")  # Extend Base functions for units of angle.
include("defaults.jl") # Submodule to flood workspace with unit types.

# Register new units and dimensions with Unitful.jl.
const localpromotion = copy(promotion)
function __init__()
    register(DimensionfulAngles)
    merge!(promotion, localpromotion)
    return nothing
end

end
